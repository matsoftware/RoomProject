%Comodini - by Mattia Campolese

 LICENZA:
 ----------------------------------------------------------------
 Copyright (C) 2005 - Mattia Campolese

 This program is free software; you can redistribute it and/or
 modify it under the terms of the GNU General Public License
 as published by the Free Software Foundation; either version 2
 of the License, or (at your option) any later version.

 This program is distributed in the hope that it will be useful,
 but WITHOUT ANY WARRANTY; without even the implied warranty of
 MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 GNU General Public License for more details.

 You should have received a copy of the GNU General Public License
 along with this program; if not, write to the Free Software
 Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.
 -----------------------------------------------------------------

 Consulta il file 'Leggimi.rtf' allegato con questo script per ulteriori
 informazioni.

 ----------------------------------------------------------------%

%comodino1 - Mattia%
Def comodino_mattia = struct:<t:<1,2,3>:<0.6,0.4,0.25>:com1_appoggio, t:3:0.5:como1,%opzionale% t:3:6.1:appoggio>
 Where 
  %blocco con cassetti%
  como1 = struct:<com1_blocco, t:<1,2,3>:<0.05,-0.2,0.02>:(com1_cassetti:1.3),
	  t:<1,2,3>:<0.05,-0.2,1.65>:(com1_cassetti:1.3), 
          t:<1,2,3>:<0.05,-0.2,3.28>:(com1_cassetti:1.3),
          t:<1,2,3>:<0.05,-0.2,4.91>:(com1_cassetti:0.25), t:<1,2,3>:<3.7,-0.02,4.25>:serratura>
    Where 
     %struttura%
     com1_blocco = cuboid:<4,5.6,5.6> color white,
     %cassetti%
     com1_cassetti(h::IsRealPos) = struct:<cassetto, t:3:h:maniglia>
      Where
       cassetto = cuboid:<3.9,0.2,h> color white,
       maniglia = struct:<back, front, inf> Acolor rgbacolor:<0.75,0.75,0.75,0>
        Where
         back = (t:<1,2,3>:<3.9,0.18,0.3> ~ r:<1,3>:(pi)):(struct:<cuboid:<3.9,0.02,0.28>, 
                (t:1:3.9 ~ r:<1,3>:(pi/2)):(mat_cyl:<0.025,3.9>:20)>),
         front = (t:<2,3>:<0,0.07> ~ r:<2,3>:pi):(struct:< cuboid:<3.9,0.02,0.1>, 
                 (t:1:3.9 ~ r:<1,3>:(pi/2)):(mat_cyl:<0.025,3.9>:20)>),
         inf = cuboid:<3.9, 0.2, 0.02>
        End
      End,
     serratura = (r:<2,3>:(pi/2)):(ring:<0.145,0.15>:<20,4> * q:0.2) Acolor rgbacolor:<0.75,0.75,0.75,0>
    End,
  %rotelle%
  com1_appoggio = struct:<rotella, t:2:5:rotella, t:1:2.8:rotella, t:<1,2>:<2.8,5>:rotella> Acolor rgbacolor:<0.24,0.24,0.24,0.24>
   Where rotella = (r:<1,2>:(pi/4) ~ r:<2,3>:(pi/2)):((ring:<0.23,0.25>:<20,4>) * q:0.2) end,
  %opzionale - libri appoggiati%
  appoggio = struct:<(t:<1,2>:<3.7,1.5> ~ r:<1,2>:(pi/1.9)):ilprincipe, 
  (t:<1,2>:<3.7,3> ~ r:<1,2>:(pi/2)):deminam>
 end;

%comodino 2 - Flavio%
Def comodino_flavio = struct:<blocco, t:<2,3>:<-0.2,3.7>:listello, 
                      t:<1,2,3>:<0.02,-0.2,0.77>:cassetto, t:<1,2,3>:<0.02,-0.2,2.24>:cassetto,
                      t:<1,2,3>:<3.5,2.75,4>:lampada_flavio>
 Where
  blocco = @2:(cuboid:<4.5,3.7,4>) texture lightwood,
  listello = cuboid:<4.5,0.2,0.3> Acolor rgbacolor:<0.96,0.6, 0.6,1>,
  cassetto = struct:<cuboid:<4.46,0.2,1.45> texture lightwood, 
  t:<1,2,3>:<1.63,-0.1,0.625>:(cuboid:<1.2,0.1,0.2> Acolor rgbacolor:<0.96,0.6,0.6,1>)>
 End;